# Make sure clang/opt are using the right flags for running passes based on the llvm version
if("${LLVM_VERSION_MAJOR}" VERSION_GREATER_EQUAL "12")
  set(OPT_USE_LEGACY_PM "-enable-new-pm=0")
  set(CLANG_USE_LEGACY_PM "-flegacy-pass-manager")
endif()

get_target_property(FUZZ_TARGET_LIBRARIES fuzz_target_lib LINK_LIBRARIES)
# Also add the fuzz target lib containing main
list(INSERT FUZZ_TARGET_LIBRARIES 0 fuzz_target_lib)

foreach(FUZZ_TARGET_LIBRARY ${FUZZ_TARGET_LIBRARIES})
    list(APPEND FUZZ_TARGET_LIBRARIES_GENEXPRS_FILES \"../lib/$<TARGET_FILE_NAME:${FUZZ_TARGET_LIBRARY}>\")
endforeach()

list(FILTER FUZZ_TARGET_LIBRARIES_GENEXPRS_FILES EXCLUDE REGEX "[.]a>\"$")
list(FILTER FUZZ_TARGET_LIBRARIES_GENEXPRS_FILES EXCLUDE REGEX "[.]lib>>\"$")

list(JOIN FUZZ_TARGET_LIBRARIES_GENEXPRS_FILES "," FUZZ_TARGET_LIBRARIES_FILES_LIST)
set(FUZZ_TARGET_LIBRARIES_FILES_LIST "[${FUZZ_TARGET_LIBRARIES_FILES_LIST}]")

set(FUZZ_TARGET_NEEDED_COMPILATION_FLAGS "${CMAKE_THREAD_LIBS_INIT} -flto")

if (LIBRT)
    set(FUZZ_TARGET_NEEDED_COMPILATION_FLAGS "${FUZZ_TARGET_NEEDED_COMPILATION_FLAGS} -lrt")
endif()

list(JOIN FUZZ_TARGET_NEEDED_COMPILATION_FLAGS " " FUZZ_TARGET_NEEDED_COMPILATION_FLAGS)

set(SERVER_FILE "../tools/$<TARGET_FILE_NAME:sbt-fizzer_server>")

set(CLIENT_FILE "../tools/$<TARGET_FILE_NAME:sbt-fizzer_client>")

set(FIZZER_INSTRUMENTER_FILE "../tools/$<TARGET_FILE_NAME:instrumenter>")


set(FIZZER_TARGET_NAME sbt-fizzer)
set(FIZZER_BUILD_FUZZ_TARGET_TARGET_NAME sbt-fizzer_build_target)
set(FIZZER_INSTRUMENT_TARGET_NAME sbt-fizzer_instrument)

configure_file(${FIZZER_TARGET_NAME}.py ${FIZZER_TARGET_NAME}.py @ONLY)
file(
    GENERATE
    OUTPUT "$<CONFIG>/${FIZZER_TARGET_NAME}"
    INPUT ${CMAKE_CURRENT_BINARY_DIR}/${FIZZER_TARGET_NAME}.py
    )

install(PROGRAMS "${CMAKE_CURRENT_BINARY_DIR}/$<CONFIG>/${FIZZER_TARGET_NAME}" DESTINATION "tools")

configure_file(${FIZZER_BUILD_FUZZ_TARGET_TARGET_NAME}.py ${FIZZER_BUILD_FUZZ_TARGET_TARGET_NAME}.py @ONLY)
file(
    GENERATE
    OUTPUT "$<CONFIG>/${FIZZER_BUILD_FUZZ_TARGET_TARGET_NAME}"
    INPUT ${CMAKE_CURRENT_BINARY_DIR}/${FIZZER_BUILD_FUZZ_TARGET_TARGET_NAME}.py
    )

install(PROGRAMS "${CMAKE_CURRENT_BINARY_DIR}/$<CONFIG>/${FIZZER_BUILD_FUZZ_TARGET_TARGET_NAME}" DESTINATION "tools")

configure_file(${FIZZER_INSTRUMENT_TARGET_NAME}.py ${FIZZER_INSTRUMENT_TARGET_NAME}.py @ONLY)
file(
    GENERATE
    OUTPUT "$<CONFIG>/${FIZZER_INSTRUMENT_TARGET_NAME}"
    INPUT ${CMAKE_CURRENT_BINARY_DIR}/${FIZZER_INSTRUMENT_TARGET_NAME}.py
    )

install(PROGRAMS "${CMAKE_CURRENT_BINARY_DIR}/$<CONFIG>/${FIZZER_INSTRUMENT_TARGET_NAME}" DESTINATION "tools")