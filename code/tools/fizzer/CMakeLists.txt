set(THIS_TARGET_NAME fizzer_${CMAKE_SYSTEM_NAME}_${CMAKE_BUILD_TYPE})

get_target_property(CLIENT_LIBRARIES client_lib LINK_LIBRARIES)
# Also add the client lib containing main
list(APPEND CLIENT_LIBRARIES client_lib)

foreach(CLIENT_LIBRARY ${CLIENT_LIBRARIES})
    list(APPEND CLIENT_LIBRARIES_GENEXPRS_FILES "${CMAKE_INSTALL_PREFIX}/lib/$<TARGET_FILE_NAME:${CLIENT_LIBRARY}>")
endforeach()

foreach(BOOST_LIBRARY ${BOOST_LIST_OF_LIBRARIES_TO_LINK_WITH})
    list(APPEND BOOST_LIBRARIES_GENEXPRS_FILES "$<TARGET_PROPERTY:${BOOST_LIBRARY},LOCATION>")
endforeach()

set(CLIENT_LIBRARIES_FILES
    ${CLIENT_LIBRARIES_GENEXPRS_FILES}
    ${BOOST_LIBRARIES_GENEXPRS_FILES}
    )

list(JOIN CLIENT_LIBRARIES_FILES " " CLIENT_LIBRARIES_FILES)

set(CLIENT_NEEDED_COMPILATION_FLAGS
    ${CMAKE_THREAD_LIBS_INIT}
    )

list(JOIN CLIENT_NEEDED_COMPILATION_FLAGS " " CLIENT_NEEDED_COMPILATION_FLAGS)

set(SERVER_FILE "${CMAKE_INSTALL_PREFIX}/tools/$<TARGET_FILE_NAME:server>")

set(FIZZER_PASS_FILE "${CMAKE_INSTALL_PREFIX}/lib/$<TARGET_FILE_NAME:fizzer_pass>")

configure_file(fizzer.py.in fizzer.py @ONLY)
file(
    GENERATE
    OUTPUT ${THIS_TARGET_NAME}
    INPUT ${CMAKE_CURRENT_BINARY_DIR}/fizzer.py
    )

install(PROGRAMS ${CMAKE_CURRENT_BINARY_DIR}/${THIS_TARGET_NAME} DESTINATION "tools")
